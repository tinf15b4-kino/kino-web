buildscript {
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.3.RELEASE")
    }
}

apply plugin: 'org.springframework.boot'

project.ext {
    cucumberVersion = '1.2.5'
    seleniumVersion = '3.4.0'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
            srcDir 'src/main/webapp'
        }
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.seleniumhq.selenium' && details.requested.name != 'htmlunit-driver') {
            //HACK: Force Selenium 3 down spring boot's throat
            details.useVersion seleniumVersion
        }
    }
}

test {
    systemProperty "kinotest.driver", System.getProperty("kinotest.driver")
    systemProperty "kinotest.seleniumHub", System.getProperty("kinotest.seleniumHub")

    environment "SMARTCINEMA_DATA_API_DIR", "${project(':data-api').projectDir}"
}

dependencies {
    compile('com.vaadin:vaadin-spring-boot-starter:1.2.0')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-jetty')
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('com.h2database:h2')
    compile('com.google.guava:guava:21.0')
    compile('com.google.code.gson:gson:2.8.0')
    compile project(':commons')
    testCompile('org.seleniumhq.selenium:selenium-java')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('info.cukes:cucumber-java:' + cucumberVersion)
    testCompile('info.cukes:cucumber-junit:' + cucumberVersion)
    testCompile('info.cukes:cucumber-spring:' + cucumberVersion)
    testCompile('junit:junit')
    testCompile('io.github.bonigarcia:webdrivermanager:1.5.+')
}
